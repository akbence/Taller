plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.7'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
	id "org.sonarqube" version "7.0.1.6134"
	id "org.springdoc.openapi-gradle-plugin" version "1.9.0"
}

group = 'hu.codemosaic'
version = '0.0.1-SNAPSHOT'
description = 'TallÃ©r backend'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('springModulithVersion', "1.4.3")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.modulith:spring-modulith-starter-core'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	implementation('org.apache.commons:commons-lang3:3.18.0')

	compileOnly 'org.projectlombok:lombok'

	runtimeOnly 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14'
	runtimeOnly 'org.springframework.modulith:spring-modulith-actuator'
	runtimeOnly 'org.springframework.modulith:spring-modulith-observability'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.modulith:spring-modulith-starter-test'
	testImplementation 'io.rest-assured:rest-assured:5.4.0'
	testImplementation 'io.rest-assured:json-path:5.4.0'
	testImplementation 'io.rest-assured:json-schema-validator:5.4.0'

	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	constraints {
		implementation('org.apache.commons:commons-lang3:3.18.0') {
			because 'Fixes CVE-2025-48924'
		}
	}
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.modulith:spring-modulith-bom:${springModulithVersion}"
	}
}

/**
 * Separate tests based on package:
 * - Unit tests: hu.codemosaic.taller.unit.*
 * - Integration tests: hu.codemosaic.taller.integration.*
 */
tasks.named('test') {
	description = "Runs unit tests only."
	group = "verification"
	useJUnitPlatform()

	// Only include unit tests (exclude integration)
	include '**/unit/**'
	exclude '**/integration/**'
}

def integrationTest = tasks.register("integrationTest", Test) {
	description = "Runs integration tests only."
	group = "verification"
	useJUnitPlatform()

	// Only include integration tests
	include '**/integration/**'

	testClassesDirs = sourceSets.test.output.classesDirs
	classpath = sourceSets.test.runtimeClasspath
}

def loadEnvFile(String path) {
	def env = [:]
	file(path).eachLine { line ->
		if (!line.trim() || line.startsWith("#")) return
		def (key, value) = line.split("=", 2)
		env[key] = value
	}
	return env
}

def localEnv = loadEnvFile("local.env")


openApi {
	apiDocsUrl = 'http://localhost:8080/v3/api-docs'
	outputDir = file("$buildDir/openapi")
	outputFileName = 'openapi.yaml'
	waitTimeInSeconds = 60

	customBootRun {
		environment.putAll(localEnv)
	}
}

tasks.named('check') {
	dependsOn integrationTest
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
		csv.required = false
	}
}

// Enable Jacoco for integration tests
tasks.withType(Test).configureEach {
	useJUnitPlatform()
	jacoco {
		enabled = true
	}
}

// Create a merged Jacoco report for SonarCloud
tasks.register('jacocoMergedReport', JacocoReport) {
	dependsOn tasks.test, tasks.integrationTest

	executionData.from = fileTree(layout.buildDirectory.get().asFile).include(
			"jacoco/test.exec",
			"jacoco/test/*.exec",
			"jacoco/integrationTest.exec",
			"jacoco/integrationTest/*.exec"
	)

	sourceSets sourceSets.main

	reports {
		xml.required = true
		xml.outputLocation = layout.buildDirectory.file("reports/jacoco/merged/jacocoMergedReport.xml")
		html.required = true
	}
}

sonar {
	properties {
		property "sonar.projectKey", "codemosaic_Taller"
		property "sonar.organization", "codemosaic"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.coverage.jacoco.xmlReportPaths", layout.buildDirectory.file("reports/jacoco/merged/jacocoMergedReport.xml").get().asFile.path
	}
}
